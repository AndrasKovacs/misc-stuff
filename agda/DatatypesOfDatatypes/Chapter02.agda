
open import Data.List
open import Data.Nat
open import Data.Product
open import Data.Unit
open import Function hiding (_$_)
open import Data.Empty
open import Data.Sum
open import Relation.Binary.PropositionalEquality

data â‹† : Set where
  Î¹   : â‹†
  _â–·_ : â‹† â†’ â‹† â†’ â‹†
infixr 5 _â–·_

data Cx (A : Set) : Set where
  ğ“”   : Cx A
  _,_ : Cx A â†’ A â†’ Cx A
infixl 4 _,_

data _âˆˆ_ (Ï„ : â‹†) : Cx â‹† â†’ Set where
  zero : âˆ€ {Î“}           â†’ Ï„ âˆˆ Î“ , Ï„
  suc  : âˆ€ {Î“ Ïƒ} â†’ Ï„ âˆˆ Î“ â†’ Ï„ âˆˆ Î“ , Ïƒ
infix 4 _âˆˆ_  

data _âŠ¢_ (Î“ : Cx â‹†) : â‹† â†’ Set where
  var :
    âˆ€ {Ï„}
    â†’ Ï„ âˆˆ Î“
    --------
    â†’ Î“ âŠ¢ Ï„

  lam :
    âˆ€ {Ïƒ Ï„}
    â†’ Î“ , Ïƒ âŠ¢ Ï„
    ------------
    â†’ Î“ âŠ¢ Ïƒ â–· Ï„

  app :
    âˆ€ {Ïƒ Ï„}
    â†’ Î“ âŠ¢ Ïƒ â–· Ï„ â†’ Î“ âŠ¢ Ïƒ
    --------------------
    â†’ Î“ âŠ¢ Ï„    
infix 3 _âŠ¢_

âŸ¦_âŸ§â‹† : â‹† â†’ Set
âŸ¦ Î¹     âŸ§â‹† = â„•
âŸ¦ Ïƒ â–· Ï„ âŸ§â‹† = âŸ¦ Ïƒ âŸ§â‹† â†’ âŸ¦ Ï„ âŸ§â‹†

âŸ¦_âŸ§c  : Cx â‹† â†’ (â‹† â†’ Set) â†’ Set
âŸ¦ ğ“”     âŸ§c V = âŠ¤
âŸ¦ Î“ , Ïƒ âŸ§c V = âŸ¦ Î“ âŸ§c V Ã— V Ïƒ

âŸ¦_âŸ§âˆˆ : âˆ€ {Î“ Ï„ V} â†’ Ï„ âˆˆ Î“ â†’  âŸ¦ Î“ âŸ§c V â†’ V Ï„
âŸ¦ zero  âŸ§âˆˆ (Î³ , t) = t
âŸ¦ suc i âŸ§âˆˆ (Î³ , s) = âŸ¦ i âŸ§âˆˆ Î³

âŸ¦_âŸ§âŠ¢ : âˆ€ {Î“ Ï„} â†’ Î“ âŠ¢ Ï„ â†’  âŸ¦ Î“ âŸ§c âŸ¦_âŸ§â‹† â†’ âŸ¦ Ï„ âŸ§â‹†
âŸ¦ var i   âŸ§âŠ¢   = âŸ¦ i âŸ§âˆˆ
âŸ¦ lam t   âŸ§âŠ¢ Î³ = Î» s â†’ âŸ¦ t âŸ§âŠ¢ (Î³ , s)
âŸ¦ app f s âŸ§âŠ¢ Î³ = âŸ¦ f âŸ§âŠ¢ Î³ (âŸ¦ s âŸ§âŠ¢ Î³)

-- alternatively : see "order preserving embeddings"
Ren Sub : Cx â‹† â†’ Cx â‹† â†’ Set
Ren Î“ Î” = âˆ€ {Ï„} â†’ Ï„ âˆˆ Î“ â†’ Ï„ âˆˆ Î”
Sub Î“ Î” = âˆ€ {Ï„} â†’ Ï„ âˆˆ Î“ â†’ Î” âŠ¢ Ï„

_<><_ : âˆ€ {A} â†’ Cx A â†’ List A â†’ Cx A
xz <>< []       = xz
xz <>< (a âˆ· as) = (xz , a) <>< as
infixl 4 _<><_

Shub : Cx â‹† â†’ Cx â‹† â†’ Set
Shub Î“ Î” = âˆ€ Î â†’ Sub (Î“ <>< Î) (Î” <>< Î)

_//_ : âˆ€ {Î“ Î”} (Î¸ : Shub Î“ Î”) {Ï„} â†’ Î“ âŠ¢ Ï„ â†’ Î” âŠ¢ Ï„
Î¸ // var i   = Î¸ [] i
Î¸ // lam t   = lam ((Î¸ âˆ˜ (_âˆ·_ _)) // t)
Î¸ // app f s = app (Î¸ // f) (Î¸ // s)

wkr : âˆ€ {Î“ Î” Ïƒ} â†’ Ren Î“ Î” â†’ Ren (Î“ , Ïƒ) (Î” , Ïƒ)
wkr r zero    = zero
wkr r (suc i) = suc (r i)

ren : âˆ€ {Î“ Î”} â†’ Ren Î“ Î” â†’ Shub Î“ Î”
ren r []      = var âˆ˜ r
ren r (_ âˆ· Î) = ren (wkr r) Î

wks : âˆ€ {Î“ Î” Ïƒ} â†’ Sub Î“ Î” â†’ Sub (Î“ , Ïƒ) (Î” , Ïƒ)
wks s zero    = var zero
wks s (suc i) = ren suc // s i

sub : âˆ€ {Î“ Î”} â†’ Sub Î“ Î” â†’ Shub Î“ Î”
sub s []      = s
sub s (_ âˆ· Î) = sub (wks s) Î

weak : âˆ€ {Î“} Î â†’ Ren Î“ (Î“ <>< Î)
weak []      = id
weak (_ âˆ· Î) = weak Î âˆ˜ suc


-- Auto lambda
------------------------------------------------------------

_<>>_ : âˆ€ {A} â†’ Cx A â†’ List A â†’ List A
ğ“”        <>> ys = ys
(xs , x) <>> ys = xs <>> (x âˆ· ys)
infixl 4 _<>>_

rev-lem : âˆ€ {A} Î“ (xs : List A) â†’ (ğ“” <>< (Î“ <>> xs)) â‰¡  ((ğ“” <>< (Î“ <>> [])) <>< xs)
rev-lem ğ“”       xs = refl
rev-lem (Î“ , x) xs rewrite rev-lem Î“ (x âˆ· xs) | rev-lem Î“ (x âˆ· []) = refl

rev-rev : âˆ€ {A} Î“ â†’ ğ“” {A} <>< (Î“ <>> []) â‰¡ Î“
rev-rev ğ“”       = refl
rev-rev (Î“ , x) rewrite rev-lem Î“ (x âˆ· []) | rev-rev Î“ = refl

rev-inj : âˆ€ {A}(Î“ Î” : Cx A) â†’ Î“ <>> [] â‰¡ Î” <>> [] â†’ Î“ â‰¡ Î”
rev-inj Î“ Î” p = substâ‚‚ _â‰¡_ (rev-rev Î“) (rev-rev Î”) (cong (_<><_ ğ“”) p)

lem : âˆ€ {A}(Î” Î“ : Cx A) Î â†’ Î” <>> [] â‰¡ Î“ <>> Î â†’ Î“ <>< Î â‰¡ Î”
lem Î” Î“ []      p = rev-inj Î“ Î” (sym p)
lem Î” Î“ (x âˆ· Î) p = lem Î” (Î“ , x) Î p

lambda :
  âˆ€ {Î“ Ïƒ Ï„}
  â†’ ((âˆ€ {Î” Î} {{_ : Î” <>> [] â‰¡ Î“ <>> (Ïƒ âˆ· Î)}} â†’ Î” âŠ¢ Ïƒ) â†’ Î“ , Ïƒ âŠ¢ Ï„)
  â†’ Î“ âŠ¢ Ïƒ â–· Ï„
lambda {Î“} f =
  lam (f (Î» {Î”} {Î} {{q}}
    â†’ subst (Î» Î“ â†’ Î“ âŠ¢ _) (lem Î” Î“ (_ âˆ· Î) q) (var (weak Î zero))))  

myTest : ğ“” âŠ¢ (Î¹ â–· Î¹) â–· (Î¹ â–· Î¹)
myTest = lambda Î» f â†’ lambda Î» x â†’ app f (app f x)

-- Normalization
------------------------------------------------------------

mutual
  data _âŠ¨_ (Î“ : Cx â‹†) : â‹† â†’ Set where
    lam : âˆ€ {Ïƒ Ï„} â†’ Î“ , Ïƒ âŠ¨ Ï„ â†’ Î“ âŠ¨ Ïƒ â–· Ï„
    _$_ : âˆ€ {Ï„} â†’ Ï„ âˆˆ Î“ â†’ Î“ âŠ¨â‹† Ï„ â†’ Î“ âŠ¨ Î¹

  data _âŠ¨â‹†_ (Î“ : Cx â‹†) : â‹† â†’ Set where
    []  : Î“ âŠ¨â‹† Î¹
    _,_ : âˆ€ {Ïƒ Ï„} â†’ Î“ âŠ¨ Ïƒ â†’ Î“ âŠ¨â‹† Ï„ â†’ Î“ âŠ¨â‹† Ïƒ â–· Ï„

infix 3 _âŠ¨_ _âŠ¨â‹†_
infix 3 _$_

_-_ : âˆ€ (Î“ : Cx â‹†) {Ï„} â†’ Ï„ âˆˆ Î“ â†’ Cx â‹†
ğ“” - ()
(Î“ , x) - zero  = Î“
(Î“ , x) - suc i = (Î“ - i) , x
infixl 6 _-_

_â‰ _ : âˆ€ {Î“ Ïƒ}(i : Ïƒ âˆˆ Î“) â†’ Ren (Î“ - i) Î“
zero  â‰  i'     = suc i'
suc i â‰  zero   = zero
suc i â‰  suc i' = suc (i â‰  i')

mutual 
  renNm : âˆ€ {Î“ Î” Ï„} â†’ Ren Î“ Î” â†’ Î“ âŠ¨ Ï„ â†’ Î” âŠ¨ Ï„
  renNm r (lam t)  = lam (renNm (wkr r) t)
  renNm r (i $ sp) = r i $ renSp r sp

  renSp : âˆ€ {Î“ Î” Ï„} â†’ Ren Î“ Î” â†’ Î“ âŠ¨â‹† Ï„ â†’ Î” âŠ¨â‹† Ï„
  renSp r []       = []
  renSp r (t , sp) = renNm r t , renSp r sp

data Veq? {Î“ Ïƒ}(i : Ïƒ âˆˆ Î“) : âˆ€ {Ï„} â†’ Ï„ âˆˆ Î“ â†’ Set where
  same : Veq? i i
  diff : âˆ€ {Ï„}(i' : Ï„ âˆˆ Î“ - i) â†’ Veq? i (i â‰  i')

veq? : âˆ€ {Î“ Ïƒ Ï„}(i : Ïƒ âˆˆ Î“)(i' : Ï„ âˆˆ Î“) â†’ Veq? i i'
veq? zero zero     = same
veq? zero (suc i') = diff i'
veq? (suc i) zero  = diff zero
veq? (suc i) (suc i') with veq? i i'
veq? (suc i) (suc .i)        | same    = same
veq? (suc i) (suc .(i â‰  i')) | diff i' = diff (suc i')

mutual 
  âŸ¨_âŸ¶_âŸ©_ : âˆ€ {Î“ Ïƒ Ï„}(i : Ïƒ âˆˆ Î“) â†’ Î“ - i âŠ¨ Ïƒ â†’ Î“ âŠ¨ Ï„ â†’ Î“ - i âŠ¨ Ï„
  âŸ¨ i âŸ¶ s âŸ© lam t    = lam (âŸ¨ suc i âŸ¶ renNm suc s âŸ© t)
  âŸ¨ i âŸ¶ s âŸ© (i' $ ts) with veq? i i'
  âŸ¨ i âŸ¶ s âŸ© .i $ ts        | same    = s $$ âŸ¨ i âŸ¶ s âŸ©â‹† ts
  âŸ¨ i âŸ¶ s âŸ© .(i â‰  i') $ ts | diff i' = i' $ âŸ¨ i âŸ¶ s âŸ©â‹† ts
  infix 2 âŸ¨_âŸ¶_âŸ©_

  âŸ¨_âŸ¶_âŸ©â‹†_ : âˆ€ {Î“ Ïƒ Ï„}(i : Ïƒ âˆˆ Î“) â†’ Î“ - i âŠ¨ Ïƒ â†’ Î“ âŠ¨â‹† Ï„ â†’ Î“ - i âŠ¨â‹† Ï„
  âŸ¨ i âŸ¶ s âŸ©â‹† []       = []
  âŸ¨ i âŸ¶ s âŸ©â‹† (t , sp) = (âŸ¨ i âŸ¶ s âŸ© t) , (âŸ¨ i âŸ¶ s âŸ©â‹† sp)

  _$$_ : âˆ€ {Î“ Ï„} â†’ Î“ âŠ¨ Ï„ â†’ Î“ âŠ¨â‹† Ï„ â†’ Î“ âŠ¨ Î¹
  s     $$ []       = s
  lam s $$ (t , sp) = (âŸ¨ zero âŸ¶ t âŸ© s) $$ sp
  infix 3 _$$_

-- Î·-expansion
------------------------------------------------------------

intros : Cx â‹† â†’ â‹† â†’ Cx â‹†
intros Î“ Î¹       = Î“
intros Î“ (Ï„ â–· Ïƒ) = intros (Î“ , Ï„) Ïƒ

renIntros : âˆ€ Î“ Ï„ â†’ Ren Î“ (intros Î“ Ï„)
renIntros Î“ Î¹       = id
renIntros Î“ (Ï„ â–· Ïƒ) = renIntros (Î“ , Ï„) Ïƒ âˆ˜ suc

expand : âˆ€ {Î“} Ï„ â†’ intros Î“ Ï„ âŠ¨ Î¹ â†’ Î“ âŠ¨ Ï„
expand Î¹       t = t
expand (Ï„ â–· Ïƒ) t = lam (expand Ïƒ t)

mutual 
  Î· : âˆ€ {Î“ Ï„} â†’ Ï„ âˆˆ Î“ â†’ Î“ âŠ¨ Ï„
  Î· {Î“}{Ï„} i = expand Ï„ (renIntros Î“ Ï„ i $ mkSpine Î“ Ï„)
  
  mkSpine : âˆ€ Î“ Ï„ â†’ intros Î“ Ï„ âŠ¨â‹† Ï„
  mkSpine Î“ Î¹       = []
  mkSpine Î“ (Ï„ â–· Ïƒ) = Î· (renIntros (Î“ , Ï„) Ïƒ zero) , mkSpine (Î“ , Ï„) Ïƒ

-- effectfully's version
-- https://github.com/effectfully/DataData/blob/master/STLC/Hereditary.agda#L96
-- how the hell does he come up with this

-- mutual
--   Î· : âˆ€ {Î“ Ï„} â†’ Ï„ âˆˆ Î“ â†’ Î“ âŠ¨ Ï„
--   Î· i = Î·* (Î» Î” s â†’ weak Î” i $ s)

--   Î·* : âˆ€ {Ï„ Î“} â†’ (âˆ€ Î” â†’ Î“ <>< Î” âŠ¨â‹† Ï„ â†’ Î“ <>< Î” âŠ¨ Î¹) â†’ Î“ âŠ¨ Ï„
--   Î·* {Î¹}     c = c [] []
--   Î·* {Ï„ â–· Ïƒ} c = lam (Î·* Î» Î” sp â†’ c (Ï„ âˆ· Î”) (Î· (weak Î” zero) , sp))

normalize : âˆ€ {Î“ Ï„} â†’ Î“ âŠ¢ Ï„ â†’ Î“ âŠ¨ Ï„
normalize (var i) = Î· i
normalize (lam t) = lam (normalize t)
normalize (app f s) with normalize f | normalize s
... | lam t | s' = âŸ¨ zero âŸ¶ s' âŸ© t

tryâ‚ : ğ“” âŠ¨ ((Î¹ â–· Î¹) â–· (Î¹ â–· Î¹)) â–· ((Î¹ â–· Î¹) â–· (Î¹ â–· Î¹))
tryâ‚ = normalize (lambda Î» x â†’ x)

churchâ‚‚ : âˆ€ {Ï„} â†’ ğ“” âŠ¢ (Ï„ â–· Ï„) â–· (Ï„ â–· Ï„)
churchâ‚‚ = lambda Î» f â†’ lambda Î» x â†’ app f (app f x)

tryâ‚‚ : ğ“” âŠ¨ (Î¹ â–· Î¹) â–· (Î¹ â–· Î¹)
tryâ‚‚ = normalize (app (app churchâ‚‚ churchâ‚‚) churchâ‚‚)

-- Normalization by evaluation
------------------------------------------------------------

data Stop Î“ Ï„ : Set where
  var : Ï„ âˆˆ Î“ â†’ Stop Î“ Ï„
  _$_ : âˆ€ {Ïƒ} â†’ Stop Î“ (Ïƒ â–· Ï„) â†’ Î“ âŠ¨ Ïƒ â†’ Stop Î“ Ï„

renSt : âˆ€ {Î“ Î” Ï„} â†’ Ren Î“ Î” â†’ Stop Î“ Ï„ â†’ Stop Î” Ï„
renSt r (var x) = var (r x)
renSt r (u $ x) = renSt r u $ renNm r x

stopSp : âˆ€ {Î“ Ï„} â†’ Stop Î“ Ï„ â†’ Î“ âŠ¨â‹† Ï„ â†’ Î“ âŠ¨ Î¹
stopSp (var x) sp = x $ sp
stopSp (u $ x) sp = stopSp u (x , sp)

mutual 
  Val : Cx â‹† â†’ â‹† â†’ Set
  Val Î“ Ï„ = Go Î“ Ï„ âŠ Stop Î“ Ï„

  Go : Cx â‹† â†’ â‹† â†’ Set
  Go Î“ Î¹       = âŠ¥
  Go Î“ (Ï„ â–· Ïƒ) = âˆ€ {Î”} â†’ Ren Î“ Î” â†’ Val Î” Ï„ â†’ Val Î” Ïƒ

renVal : âˆ€ {Î“ Î”} Ï„ â†’ Ren Î“ Î” â†’ Val Î“ Ï„ â†’ Val Î” Ï„
renVal Î¹ ren (injâ‚ ())
renVal (Ï„ â–· Ïƒ) ren (injâ‚ r') = injâ‚ (Î» r â†’ r' (r âˆ˜ ren))
renVal Ï„       ren (injâ‚‚ y)  = injâ‚‚ (renSt ren y)

renVals : âˆ€ Î¸ {Î“ Î”} â†’ Ren Î“ Î” â†’ âŸ¦ Î¸ âŸ§c (Val Î“) â†’ âŸ¦ Î¸ âŸ§c (Val Î”)
renVals ğ“”       ren c       = tt
renVals (Î¸ , x) ren (v , c) = renVals Î¸ ren v , renVal x ren c

idEnv : âˆ€ Î“ â†’ âŸ¦ Î“ âŸ§c (Val Î“)
idEnv ğ“”       = tt
idEnv (Î“ , x) = renVals Î“ suc (idEnv Î“) , injâ‚‚ (var zero)

quo : âˆ€ {Î“} Ï„ â†’ Val Î“ Ï„ â†’ Î“ âŠ¨ Ï„
quo Î¹ (injâ‚ ())
quo (Ï„ â–· Ïƒ) (injâ‚ f) = lam (quo Ïƒ (f suc (injâ‚‚ (var zero))))
quo {Î“} Ï„   (injâ‚‚ s) = expand _ (stopSp (renSt (renIntros Î“ Ï„) s) (mkSpine _ _))

apply : âˆ€ {Î“ Ïƒ Ï„} â†’ Val Î“ (Ïƒ â–· Ï„) â†’ Val Î“ Ïƒ â†’ Val Î“ Ï„
apply (injâ‚ f) s = f id s
apply (injâ‚‚ x) s = injâ‚‚ (x $ quo _ s)

eval : âˆ€ {Î“ Î” Ï„} â†’ Î“ âŠ¢ Ï„ â†’ âŸ¦ Î“ âŸ§c (Val Î”) â†’ Val Î” Ï„
eval (var i)   Î³ = âŸ¦ i âŸ§âˆˆ Î³
eval (lam t)   Î³ = injâ‚ (Î» r v â†’ eval t (renVals _ r Î³ , v))
eval (app f x) Î³ = apply (eval f Î³) (eval x Î³)

normByEval : âˆ€ {Î“ Ï„} â†’ Î“ âŠ¢ Ï„ â†’ Î“ âŠ¨ Ï„
normByEval {Î“}{Ï„} t = quo Ï„ (eval t (idEnv Î“))

