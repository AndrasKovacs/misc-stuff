{-# OPTIONS --no-eta --rewriting --without-K #-}

module STLC.Norm.Methods3 where

open import STLC.lib

open import STLC.Derived
open import STLC.Syntax
open import STLC.Norm.Methods1
open import STLC.Norm.CoeLemmas

,∘ᴹ :
  ∀ {Γ} {Γᴹ : Conᴹ Γ} {Δ} {Δᴹ : Conᴹ Δ}
    {Σ} {Σᴹ : Conᴹ Σ} {δ : Tms Γ Δ} {δᴹ : Tmsᴹ Γᴹ Δᴹ δ}
    {σ : Tms Σ Γ} {σᴹ : Tmsᴹ Σᴹ Γᴹ σ} {A} {Aᴹ : Tyᴹ A}
    {a} {aᴹ : Tmᴹ Γᴹ Aᴹ a}
    -- (δᴹ ,ₛᴹ aᴹ) ∘ᴹ σᴹ ≡[ ap (Tmsᴹ Σᴹ (Δᴹ ,ᴹ Aᴹ)) ,∘ ]≡ δᴹ ∘ᴹ σᴹ ,ₛᴹ aᴹ [ σᴹ ]ᴹ
    → _∘ᴹ_ {Σᴹ = Δᴹ ,ᴹ Aᴹ} (_,ₛᴹ_ {Δᴹ = Δᴹ} δᴹ {Aᴹ = Aᴹ} aᴹ) σᴹ
        ≡[ ap (Tmsᴹ Σᴹ (Δᴹ ,ᴹ Aᴹ)) ,∘ ]≡
      _,ₛᴹ_ {Δᴹ = Δᴹ} (_∘ᴹ_ {Σᴹ = Δᴹ} δᴹ σᴹ) {Aᴹ = Aᴹ} (_[_]ᴹ {Aᴹ = Aᴹ} aᴹ σᴹ)

,∘ᴹ {Γ}{Γᴹ}{Δ}{Δᴹ}{Σ}{Σᴹ}{δ}{δᴹ}{σ}{σᴹ}{A}{Aᴹ}{a}{aᴹ} =
  Tmsᴹ-≡-intro {Σ}{Σᴹ}{Δ , A}{Δᴹ ,ᴹ Aᴹ} ,∘ λ Ξ γ γᴹ
  → ap-◾ (ap (λ σ₁ → Δᴹ (π₁ (σ₁ ∘ γ)) × Aᴹ (π₂ (σ₁ ∘ γ))) ,∘)
         (ap (λ σ₁ → Δᴹ (π₁ σ₁) × Aᴹ (π₂ σ₁)) (ass ⁻¹))
  ◾ ap-◾
      (ap (λ σ₁ → Δᴹ (π₁ σ₁) × Aᴹ (π₂ σ₁)) (ass ⁻¹) ◾
        ap (λ σ₁ → Δᴹ (π₁ (σ₁ ∘ γ)) × Aᴹ (π₂ (σ₁ ∘ γ))) ,∘)
      (,ₛᴹ-coe Δᴹ Aᴹ)
  ◾ UIP-coe
      (,ₛᴹ-coe Δᴹ Aᴹ ◾
        (ap (λ σ₁ → Δᴹ (π₁ σ₁) × Aᴹ (π₂ σ₁)) (ass ⁻¹) ◾
        ap (λ σ₁ → Δᴹ (π₁ (σ₁ ∘ γ)) × Aᴹ (π₂ (σ₁ ∘ γ))) ,∘))
      (ap2 _×_ (ap Δᴹ (ass ⁻¹)) (ap Aᴹ ([][] ⁻¹)) ◾
        ,ₛᴹ-coe Δᴹ Aᴹ)               
  ◾ ap-◾ (,ₛᴹ-coe Δᴹ Aᴹ) (ap2 _×_ (ap Δᴹ (ass ⁻¹)) (ap Aᴹ ([][] ⁻¹))) ⁻¹
  ◾ ap (coe (,ₛᴹ-coe Δᴹ Aᴹ)) (coe-× (ap Δᴹ (ass ⁻¹)) (ap Aᴹ ([][] ⁻¹))) ⁻¹

,β₁ᴹ :
  ∀ {Γ} {Γᴹ : Conᴹ Γ} {Δ} {Δᴹ : Conᴹ Δ} {A}{Aᴹ : Tyᴹ A}
    {δ : Tms Γ Δ} {δᴹ : Tmsᴹ Γᴹ Δᴹ δ} {a : Tm Γ A}{aᴹ : Tmᴹ Γᴹ Aᴹ a}
    -- π₁ᴹ (δᴹ ,ₛᴹ aᴹ) ≡[ ap (Tmsᴹ Γᴹ Δᴹ) ,β₁ ]≡ δᴹ
    → π₁ᴹ {Δᴹ = Δᴹ}{Aᴹ = Aᴹ} (_,ₛᴹ_ {Δᴹ = Δᴹ} δᴹ {Aᴹ = Aᴹ} aᴹ) ≡[ ap (Tmsᴹ Γᴹ Δᴹ) ,β₁ ]≡ δᴹ

,β₁ᴹ {Γ}{Γᴹ}{Δ}{Δᴹ}{A}{Aᴹ}{δ}{δᴹ}{a}{aᴹ} =
  Tmsᴹ-≡-intro {Γ}{Γᴹ}{Δ}{Δᴹ} ,β₁ λ Σ σ σᴹ
  → ap-◾ (ap (λ σ₁ → Δᴹ (σ₁ ∘ σ)) ,β₁) (ap Δᴹ (π₁∘ ⁻¹))
  ◾ ap (λ x → coe (ap Δᴹ (π₁∘ ⁻¹) ◾ ap (λ σ₁ → Δᴹ (σ₁ ∘ σ)) ,β₁) (proj₁ x))
       (coe-× (ap Δᴹ ((ap π₁ ,∘ ◾ ,β₁) ⁻¹)) (ap Aᴹ ((ap π₂ ,∘ ◾ ,β₂) ⁻¹))) ⁻¹
  ◾ ap-◾ (ap Δᴹ (π₁∘ ⁻¹) ◾ ap (λ σ₁ → Δᴹ (σ₁ ∘ σ)) ,β₁)
         (ap Δᴹ ((ap π₁ ,∘ ◾ ,β₁) ⁻¹))
  ◾ UIP-coe
       (ap Δᴹ ((ap π₁ ,∘ ◾ ,β₁) ⁻¹) ◾ (ap Δᴹ (π₁∘ ⁻¹) ◾ ap (λ σ₁ → Δᴹ (σ₁ ∘ σ)) ,β₁))
       refl

,ηᴹ :
  ∀ {Γ} {Γᴹ : Conᴹ Γ} {Δ} {Δᴹ : Conᴹ Δ} {A}{Aᴹ : Tyᴹ A}
    {δ : Tms Γ (Δ , A)} {δᴹ : Tmsᴹ Γᴹ (Δᴹ ,ᴹ Aᴹ) δ}
  -- π₁ᴹ δᴹ ,ₛᴹ π₂ᴹ δᴹ ≡[ ap (Tmsᴹ Γᴹ (Δᴹ ,ᴹ Aᴹ)) ,η ]≡ δᴹ
  → _,ₛᴹ_ {Δᴹ = Δᴹ} (π₁ᴹ {Δᴹ = Δᴹ}{Aᴹ = Aᴹ} δᴹ) {Aᴹ = Aᴹ} (π₂ᴹ {Δᴹ = Δᴹ}{Aᴹ = Aᴹ} δᴹ)
      ≡[ ap (Tmsᴹ Γᴹ (Δᴹ ,ᴹ Aᴹ)) ,η ]≡ δᴹ
,ηᴹ {Γ}{Γᴹ}{Δ}{Δᴹ}{A}{Aᴹ}{δ}{δᴹ} =
  Tmsᴹ-≡-intro {Γ}{Γᴹ}{Δ , A}{Δᴹ ,ᴹ Aᴹ} ,η λ Σ σ σᴹ
  → ap
     (λ x → coe
        (ap (λ σ₁ → Δᴹ (π₁ (σ₁ ∘ σ)) × Aᴹ (π₂ (σ₁ ∘ σ))) ,η)
        (coe (,ₛᴹ-coe Δᴹ Aᴹ) x))
     (coe-× (ap Δᴹ (π₁∘ ⁻¹)) (ap Aᴹ (π₂[] ⁻¹)))
   ◾ ap-◾ (ap (λ σ₁ → Δᴹ (π₁ (σ₁ ∘ σ)) × Aᴹ (π₂ (σ₁ ∘ σ))) ,η) (,ₛᴹ-coe Δᴹ Aᴹ)
   ◾ ap-◾ (,ₛᴹ-coe Δᴹ Aᴹ ◾ ap (λ σ₁ → Δᴹ (π₁ (σ₁ ∘ σ)) × Aᴹ (π₂ (σ₁ ∘ σ))) ,η)
           (ap2 _×_ (ap Δᴹ (π₁∘ ⁻¹)) (ap Aᴹ (π₂[] ⁻¹)))
   ◾ UIP-coe
      (ap2 _×_ (ap Δᴹ (π₁∘ ⁻¹)) (ap Aᴹ (π₂[] ⁻¹)) ◾
       (,ₛᴹ-coe Δᴹ Aᴹ ◾
        ap (λ σ₁ → Δᴹ (π₁ (σ₁ ∘ σ)) × Aᴹ (π₂ (σ₁ ∘ σ))) ,η))
      refl

,β₂ᴹ :
  ∀ {Γ} {Γᴹ : Conᴹ Γ} {Δ} {Δᴹ : Conᴹ Δ} {A}{Aᴹ : Tyᴹ A}
    {δ : Tms Γ Δ} {δᴹ : Tmsᴹ Γᴹ Δᴹ δ} {a}{aᴹ : Tmᴹ Γᴹ Aᴹ a}
  -- π₂ᴹ (δᴹ ,ₛᴹ aᴹ) ≡[ ap (Tmᴹ Γᴹ Aᴹ) ,β₂ ]≡ aᴹ
  → π₂ᴹ {Δᴹ = Δᴹ}{Aᴹ = Aᴹ}(_,ₛᴹ_ {Δᴹ = Δᴹ} δᴹ {Aᴹ = Aᴹ} aᴹ) ≡[ ap (Tmᴹ Γᴹ Aᴹ) ,β₂ ]≡ aᴹ
,β₂ᴹ {Γ}{Γᴹ}{Δ}{Δᴹ}{A}{Aᴹ}{δ}{δᴹ}{a}{aᴹ} =
  Tmᴹ-≡-intro {Γ}{Γᴹ}{A}{Aᴹ} ,β₂ λ Σ σ σᴹ
  → ap
      (λ x → coe (ap (λ t → Aᴹ (t [ σ ])) ,β₂)
        (coe (ap Aᴹ (π₂[] ⁻¹))(proj₂ x)))
      (coe-× (ap Δᴹ ((ap π₁ ,∘ ◾ ,β₁) ⁻¹)) (ap Aᴹ ((ap π₂ ,∘ ◾ ,β₂) ⁻¹))) ⁻¹
  ◾ ap-◾ (ap (λ t → Aᴹ (t [ σ ])) ,β₂) (ap Aᴹ (π₂[] ⁻¹))
  ◾ ap-◾ (ap Aᴹ (π₂[] ⁻¹) ◾ ap (λ t → Aᴹ (t [ σ ])) ,β₂)
         (ap Aᴹ ((ap π₂ ,∘ ◾ ,β₂) ⁻¹))
  ◾ UIP-coe
      (ap Aᴹ ((ap π₂ ,∘ ◾ ,β₂) ⁻¹) ◾
       (ap Aᴹ (π₂[] ⁻¹) ◾ ap (λ t → Aᴹ (t [ σ ])) ,β₂))
      refl
 
